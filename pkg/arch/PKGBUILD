# Maintainer: Sebastian Lau <lauseb644 _at_ gmail _dot_ com>
pkgname="liblogcpp-git"
pkgver=1.8.1.r0.g39adcf1
pkgrel=1
pkgdesc="A intuitive and highly customizable LGPL library for logging with C++."
arch=('x86_64' 'i686' 'armv6h' 'armv7h')
url="https://git.0ptr.de/nullptr_t/liblogcpp"
license=('LGPL3')
conflicts=('liblogcpp')
provides=('liblogcpp=$pkgver')
replaces=('liblogcpp')
depends=()
makedepends=('cmake' 'qt5-base')
source=("git+https://git.0ptr.de/nullptr_t/liblogcpp.git")
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/liblogcpp"
	git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$srcdir"
	[[ -e build ]] && rm -r build
	mkdir build
}

build() {
	cd "$srcdir/build"
	
	cmake -DCMAKE_INSTALL_PREFIX:PATH="/usr" \
		-DLOGCPP_INSTALL_LIBS=ON \
		-DLOGCPP_ENABLE_QT_SUPPORT=ON \
		"../liblogcpp"
	
	make
}
	
package() {
	cd "$srcdir/build"
	make DESTDIR="${pkgdir}" install
}

